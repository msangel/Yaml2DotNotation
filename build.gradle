buildscript {

}

apply plugin: "java"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "jacoco"

group 'io.github.msangel'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.8'
    testCompile 'org.hamcrest:hamcrest-core:2.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'
}


wrapper {
    doLast {
        delete "${projectDir}/gradlew.bat"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "${project.rootDir}/repos/releases"
            def snapshotsRepoUrl = "${project.rootDir}/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
   }
}

tasks.jacocoTestCoverageVerification.dependsOn(jacocoTestReport)
tasks.publish.dependsOn(jacocoTestCoverageVerification)
tasks.publish.dependsOn(build)
tasks.install.dependsOn(build)

defaultTasks 'install'
